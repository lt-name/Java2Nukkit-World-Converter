plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
   // id 'com.jfrog.bintray' version '1.8.4'
    id 'maven'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'org.ajoberstar.git-publish' version '2.1.1'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

group 'br.com.gamemods'
version '3.1.0-SNAPSHOT'

sourceSets.main.java.srcDirs = ["src/main/kotlin"]
sourceSets.test.java.srcDirs = ["src/test/kotlin"]

sourceCompatibility = 1.8
targetCompatibility = sourceCompatibility

repositories {
    jcenter()
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url 'https://dl.bintray.com/orangy/maven' }
    maven { url 'https://repo.cuba-platform.com' }
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61'
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect'
    implementation group: 'net.md-5', name: 'bungeecord-chat', version: '1.15-SNAPSHOT'
    compile 'br.com.gamemods:region-manipulator:2.0.0'
    //implementation 'org.jetbrains.kotlinx:kotlinx-kotlinx.cli-jvm:0.3.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'jar'
            groupId project.group
            artifactId 'java2nukkit-world-converter'
            version project.version
            name project.name
            description "Converts worlds from Minecraft 1.14+ Java Edition to the newest Nukkit MCA file format"
            url "https://github.com/GameModsBR/Java2Nukkit-World-Converter"
            inceptionYear '2019'
            developers {
                developer {
                    id = 'joserobjr'
                    name = 'José Roberto de Araújo Júnior'
                    email = 'joserobjr@gamemods.com.br'
                }
            }
            scm {
                connection "https://github.com/GameModsBR/Java2Nukkit-World-Converter.git"
                developerConnection "https://github.com/GameModsBR/Java2Nukkit-World-Converter.git"
                url "https://github.com/GameModsBR/Java2Nukkit-World-Converter"
            }
        }
    }
}

jar {
    from file("$projectDir/LICENSE")
}

shadowJar {
    archiveClassifier.set('kotlinx.cli')
    manifest {
        attributes 'Main-Class': 'br.com.gamemods.j2nwc.WorldConverterCLI'
    }
    from file("$projectDir/README.md")
    from file("$projectDir/CHANGELOG.md")
    from file("$projectDir/REPLACEMENTS.md")
    from file("$projectDir/MISSING_FEATURES.md")
}

build.finalizedBy shadowJar
